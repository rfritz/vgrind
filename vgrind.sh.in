#!/bin/csh -f
#
#	$NetBSD: vgrind.sh,v 1.5 2003/08/18 14:10:22 pooka Exp $
#
# Copyright (c) 1980, 1993
#	The Regents of the University of California.  All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
# 1. Redistributions of source code must retain the above copyright
#    notice, this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright
#    notice, this list of conditions and the following disclaimer in the
#    documentation and/or other materials provided with the distribution.
# 3. All advertising materials mentioning features or use of this software
#    must display the following acknowledgement:
#	This product includes software developed by the University of
#	California, Berkeley and its contributors.
# 4. Neither the name of the University nor the names of its contributors
#    may be used to endorse or promote products derived from this software
#    without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE REGENTS AND CONTRIBUTORS ``AS IS'' AND
# ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED.  IN NO EVENT SHALL THE REGENTS OR CONTRIBUTORS BE LIABLE
# FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
# DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS
# OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
# HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
# LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY
# OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
#
#       @(#)vgrind.sh	8.1 (Berkeley) 6/6/93
#

set voptions=
set options=
set lpoption="-l"
set files=
set f=''
set head=""
set vf=@VF@
set tm=@TMDIR@
set vmacs=vgrind.tmac

while ($#argv > 0)
    switch ($1:q)

    case -f:
	set f='filter'
	set options = "$options $1:q"
	shift
	breaksw

    case -t:
	set lpoption=
	shift
	breaksw

    case -o*:
	set voptions="$voptions $1:q"
	shift
	breaksw

    case -d:
	if ($#argv < 2) then
	    echo "vgrind: $1:q option must have argument"
	    exit 1
	else
	    set options = ($options $1:q $2)
	    shift
	    shift
	endif
	breaksw
			
    case -h:
	if ($#argv < 2) then
	    echo "vgrind: $1:q option must have argument"
	    exit 1
	else
	    set head="$2"
	    shift
	    shift
	endif
	breaksw
			
    case -*:
	set options = "$options $1:q"
	shift
	breaksw

    default:
	set files = "$files $1:q"
	shift
	breaksw
    endsw
end

set rc=0
if (-r index) then
    echo > nindex
    foreach i ($files)
	#	make up a sed delete command for filenames
	#	being careful about slashes.
	echo "? $i ?d" | sed -e "s:/:\\/:g" -e "s:?:/:g" >> nindex
    end
    sed -f nindex index >xindex
    if ($f == 'filter') then
	if ("$head" != "") then
	    $vf $options -h "$head" $files | cat $tm/$vmacs -
	    set rc=$status
	else
	    $vf $options $files | cat $tm/$vmacs -
	    set rc=$status
	endif
    else
	if ("$head" != "") then
	    $vf $options -h "$head" $files | \
		sh -c "groff -Tps $lpoption -C -rx1 $voptions -i -M $tm -mvgrind 2>> xindex"
	    set rc=$status
	else
	    $vf $options $files | \
		sh -c "groff -Tps $lpoption -C -rx1 $voptions -i -M $tm -mvgrind 2>> xindex"
	    set rc=$status
	endif
    endif
    sort -df +0 -2 xindex >index
    rm nindex xindex
else
    if ($f == 'filter') then
	if ("$head" != "") then
	    $vf $options -h "$head" $files | cat $tm/$vmacs -
	    set rc=$status
	else
	    $vf $options $files | cat $tm/$vmacs -
	    set rc=$status
	endif
    else
	if ("$head" != "") then
	    $vf $options -h "$head" $files | groff -Tps $lpoption -C -i $voptions -M $tm -mvgrind
	    set rc=$status
	else
	    $vf $options $files | groff -Tps $lpoption -C -i $voptions -M $tm -mvgrind
	    set rc=$status
	endif
    endif
endif

exit $rc
